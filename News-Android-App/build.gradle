apply plugin: 'com.android.application'

android {

    /*
    signingConfigs {
        debug {
            storeFile file("~/.android/debug.keystore")
        }
    }*/

    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            shrinkResources true
            minifyEnabled true
            // TODO check if proguard-android.txt is used at all.. it's supposed to get it from the sdk. so we can delete it in this repo!
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            pseudoLocalesEnabled true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            //signingConfig signingConfigs.debug
        }
    }



    flavorDimensions "default"

    productFlavors {
        // 100% Open-Source Edition
        oss {
            dimension "default"
        }
        // 99% Open-source edition:  uses the YouTube Player Jar
        extra {
            dimension "default"
        }
    }

    lintOptions {
        // translations are imported from transifex, so no need to check here
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        disable 'MissingQuantity'
        disable 'InconsistentArrays'
        disable 'TypographyEllipsis'

        checkReleaseBuilds false

        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:

        abortOnError false
    }

    sourceSets {
        main {
            aidl.srcDirs = ['src/main/java']
        }
    }


    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://guardian.github.com/maven/repo-releases' } //needed for com.gu:option:1.3 in Android-DirectoryChooser
    maven { url  "http://dl.bintray.com/lukaville/maven" } //Needed for com.nbsp:library:1.02 in Material File Picker
    maven { url "https://jitpack.io" }

}

final SUPPORT_VERSION = '27.1.1'

dependencies {
    // core android studio module
    //compile project(':core')
    // You must install or update the Google Repository through the SDK manager to use this dependency.
    // The Google Repository (separate from the corresponding library) can be found in the Extras category.
    // implementation 'com.google.android.gms:play-services:4.2.42'
    implementation "com.github.nextcloud:android-SingleSignOn:packageRenaming-SNAPSHOT"
    implementation "com.android.support:support-v4:${SUPPORT_VERSION}"
    implementation "com.android.support:support-compat:${SUPPORT_VERSION}"
    implementation "com.android.support:appcompat-v7:${SUPPORT_VERSION}"
    implementation "com.android.support:design:${SUPPORT_VERSION}"
    implementation "com.android.support:palette-v7:${SUPPORT_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_VERSION}"
    implementation "com.android.support:customtabs:${SUPPORT_VERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORT_VERSION}"
    implementation 'de.mrmaffen:holocircularprogressbar:1.0.1'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    compileOnly 'com.google.auto.value:auto-value:1.5.2'
    annotationProcessor 'com.google.auto.value:auto-value:1.5.2'

    implementation 'com.sothree.slidinguppanel:library:3.2.1'

    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'de.greenrobot:greendao:2.0.0'
    implementation 'de.greenrobot:greendao-generator:2.0.0'
    //implementation 'org.freemarker:freemarker:2.3.23' //Required for DAO generation
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'com.github.gabrielemariotti.changeloglib:changelog:2.0.0'
    implementation 'org.jsoup:jsoup:1.8.3'
    implementation ('net.rdrei.android.dirchooser:library:3.0@aar') { transitive = true; }


    implementation 'com.google.dagger:dagger:2.16'
    annotationProcessor "com.google.dagger:dagger-compiler:2.16"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    implementation 'io.reactivex.rxjava2:rxjava:2.1.4'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.8.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    implementation project(path: ':MaterialShowcaseView:library', configuration: 'default')
    implementation 'com.nbsp:library:1.02' // MaterialFilePicker

    extraImplementation 'com.github.tommus:youtube-android-player-api:1.2.2'

    testImplementation 'org.robolectric:robolectric:3.0-rc3'
    testImplementation 'junit:junit:4.12'
    testImplementation("org.mockito:mockito-core:1.10.19") {
        exclude group: 'org.hamcrest'
    }
    testImplementation 'com.google.dexmaker:dexmaker:1.2'
    testImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.8.0'

    //androidTestCompile 'com.squareup.okhttp:mockwebserver:2.5.0'
    // https://mvnrepository.com/artifact/com.squareup.okhttp3/mockwebserver
    androidTestImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '3.8.0'


    androidTestImplementation "com.android.support:support-annotations:${SUPPORT_VERSION}"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:2.2') {
        // this library uses the newest app compat v22 but the espresso contrib still v21.
        // you have to specifically exclude the older versions of the contrib library or
        // there will be some conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

    androidTestImplementation 'tools.fastlane:screengrab:1.0.0'
    androidTestImplementation("org.mockito:mockito-core:1.10.19") {
        exclude group: 'org.hamcrest'
    }
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
}
